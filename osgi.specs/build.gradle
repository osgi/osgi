/*
 * osgi.specs Gradle build script
 */
import org.apache.tools.ant.filters.ReplaceTokens

version bnd('osgi.version')

ext.fop_home = file("${bnd.licensed}/fop")
ext.fop_classpath = files(fileTree('dir': new File(fop_home, 'lib'), 'include': '*.jar'),
  fileTree('dir': new File(fop_home, 'build'), 'includes': ['fop.jar', 'fop-hyph.jar']))

task javadoc('overwrite': true, 'type': Javadoc) {
  description 'Build the docbook flavored javadoc.'
  group 'documentation'
  destinationDir = new File(buildDir, 'javadoc')
  ext.javadocxml = new File(destinationDir, 'javadoc.xml')
  ext.stylesheet = file('docbook/xsl/javadoc2docbook.xsl')
  classpath = compileJava.classpath
  inputs.files compileJava, classpath, stylesheet
  source bnd.allSrcDirs
  bnd.javadoc_specs.split(/\s*,\s*/).each {
    include it.replace('.','/')+'/**/*.java'
  }
  configure(options) {
    memberLevel = JavadocMemberLevel.PROTECTED
    encoding = 'UTF-8'
    title = ''
    doclet 'org.osgi.tools.xmldoclet.XmlDoclet'
    docletpath compileJava.destinationDir
  }
  doLast {
    ant.xslt('style': stylesheet,
      'in': javadocxml,
      'out': "${destinationDir}/docbook/javadoc.txt") {
      classpath('location': bnd.saxon)
      factory('name': 'com.icl.saxon.TransformerFactoryImpl')
      param('name': 'destdir', 'expression': "${destinationDir}/docbook")
      param('name': 'ddf.only', 'expression': '0')
    }
    ant.xslt('style': stylesheet,
      'in': javadocxml,
      'out': "${destinationDir}/docbook/ddf.txt") {
      classpath('location': bnd.saxon)
      factory('name': 'com.icl.saxon.TransformerFactoryImpl')
      param('name': 'destdir', 'expression': "${destinationDir}/docbook")
      param('name': 'ddf.only', 'expression': '1')
    }
  }
}

task('rdmtsummary') {
  description 'Build the docbook flavored Residential DMT summary.'
  group 'documentation'
  ext.destinationDir = new File(buildDir, 'residentialdmt')
  ext.treesummaryxml = new File(destinationDir, 'treesummary.xml')
  inputs.files parent.project('dmforest').sourceSets.main.allJava, sourceSets.main.runtimeClasspath
  outputs.dir destinationDir
  doFirst {
    project.mkdir(destinationDir)
  }
  doLast {
    treesummaryxml.withOutputStream { os ->
      javaexec {
        classpath sourceSets.main.runtimeClasspath
        main = 'org.osgi.tools.dmt.TreeSummary'
        standardOutput new BufferedOutputStream(os)
        args 'org.osgi.dmt.residential.$'
      }.assertNormalExitValue()
    }
  }
}

task('xmlns', 'type': Copy) {
  description 'Build the docbook flavored schemas.'
  group 'documentation'
  from parent.file('xmlns')
  into new File(buildDir, 'xmlns')
  include '**/*.xsd'
  includeEmptyDirs = false
  doLast {
    ant.replaceregexp('match': /.*(<(\w+:)?schema)/,
      'replace': /\1/,
      'flags': 's',
      'encoding': 'UTF-8') {
      fileset('dir': destinationDir, 'includes': '**/*.xsd')
    }
  }
}

tasks.addRule('Pattern: <name>.xml: Generate the docbook xml file for the book <name>.') { taskName ->
  if (taskName.endsWith('.xml') && !taskName.endsWith('-diff.xml')) {
    task(taskName) {
      ext.book = taskName - '.xml'
      description "Generate the docbook xml file for the book ${book}."
      group 'documentation'
      ext.destinationDir = new File(buildDir, 'book')
      ext.stylesheet = file('docbook/xsl/identity.xsl')
      ext.bookxml = new File(destinationDir, taskName)
      inputs.files stylesheet, fileTree('dir': 'docbook', 'include': '**/*.xml'), sourceSets.main.runtimeClasspath
      outputs.file bookxml
      doFirst {
        project.mkdir(destinationDir)
      }
      doLast {
        ant.xslt('style': stylesheet,
          'in': "docbook/${book}/book.xml",
          'out': bookxml,
          'force': true) {
          sysproperty('key': 'org.apache.xerces.xni.parser.XMLParserConfiguration',
            'value': 'org.apache.xerces.parsers.XIncludeParserConfiguration')
          classpath {
            fileset('dir': "${fop_home}/lib") {
              include('name': 'xercesImpl*.jar')
              include('name': 'xml-apis*.jar')
            }
            pathelement('location': bnd.saxon)
          }
          factory('name': 'com.icl.saxon.TransformerFactoryImpl')
        }
        javaexec {
          classpath sourceSets.main.runtimeClasspath
          main = 'org.osgi.tools.xmlvalidation.XmlValidator'
          args file("${bnd.licensed}/docbook-xsd/docbook.xsd")
          args bookxml
        }.assertNormalExitValue()
      }
    }
  }
}

configure(tasks.getByName('core.xml')) {
  [javadoc, xmlns].each {
    inputs.files it
  }
}
configure(tasks.getByName('cmpn.xml')) {
  [javadoc, xmlns].each {
    inputs.files it
  }
}
configure(tasks.getByName('enterprise.xml')) {
  [javadoc, xmlns].each {
    inputs.files it
  }
}
configure(tasks.getByName('residential.xml')) {
  [javadoc, xmlns, rdmtsummary].each {
    inputs.files it
  }
}

tasks.addRule('Pattern: <name>-diff.xml: Generate the docbook diff xml file for the book <name>.') { taskName ->
  if (taskName.endsWith('-diff.xml')) {
    task(taskName) {
      ext.book = taskName - '-diff.xml'
      description "Generate the docbook diff xml file for the book ${book}."
      group 'documentation'
      def booktask = tasks.getByName("${book}.xml")
      ext.destinationDir = new File(buildDir, 'book')
      ext.stylesheet = file("${bnd.licensed}/diffmk/style/docbook.xsl")
      ext.baselinexml = file("baseline/${book}.xml")
      ext.bookxml = new File(destinationDir, taskName)
      inputs.files booktask, stylesheet, baselinexml
      outputs.file bookxml
      doFirst {
        project.mkdir(destinationDir)
      }
      doLast {
        def tmpxml = new File(temporaryDir, 'diff.xml')
        javaexec {
          classpath "${bnd.licensed}/diffmk/bin/diffmk.jar"
          main = 'net.sf.diffmk.DiffMk'
          minHeapSize = '1024m'
          maxHeapSize = '2048m'
          args '--verbose'
          args '1'
          args '--words'
          args baselinexml
          args booktask.bookxml
          args tmpxml
        }.assertNormalExitValue()
        ant.xslt('style': stylesheet,
          'in': tmpxml,
          'out': bookxml) {
          classpath('location': bnd.saxon)
          factory('name': 'com.icl.saxon.TransformerFactoryImpl')
        }
      }
    }
  }
}

tasks.addRule('Pattern: <name>.fo: Generate the docbook fo file for the book <name>.') { taskName ->
  if (taskName.endsWith('.fo')) {
    task(taskName) {
      ext.book = taskName - '.fo'
      description "Generate the docbook fo file for the book ${book}."
      group 'documentation'
      def booktask = tasks.getByName("${book}.xml")
      ext.destinationDir = new File(buildDir, 'fo')
      ext.stylesheet = file('docbook/xsl/custom-fo.xsl')
      ext.bookfo = new File(destinationDir, taskName)
      ext.watermark = 'docbook/graphics/draft.svg'
      inputs.files booktask, stylesheet
      outputs.file bookfo
      doFirst {
        project.mkdir(destinationDir)
      }
      doLast {
        ant.xslt('style': stylesheet,
          'in': booktask.bookxml,
          'out': bookfo) {
          classpath('location': bnd.saxon)
          factory('name': 'com.icl.saxon.TransformerFactoryImpl')
          param('name': 'draft.watermark.image', 'expression': watermark)
          param('name': 'copyright.year', 'expression': "2000, ${bnd('copyright.year')}")
        }
      }
    }
  }
}

configure(tasks.getByName('core.fo')) {
  ext.watermark = 'docbook/graphics/proposed-final-draft.svg'
}
configure(tasks.getByName('cmpn.fo')) {
  ext.watermark = 'docbook/graphics/proposed-final-draft.svg'
}

tasks.addRule('Pattern: <name>.pdf: Generate the docbook pdf file for the book <name>.') { taskName ->
  if (taskName.endsWith('.pdf')) {
    task(taskName) {
      ext.book = taskName - '.pdf'
      description "Generate the docbook pdf file for the book ${book}."
      group 'documentation'
      def fotask = tasks.getByName("${book}.fo")
      ext.destinationDir = buildDir
      ext.bookpdf = new File(destinationDir, "osgi.${book}-${version}.pdf")
      def fontbasedir = file('fonts')
      def fopxconf = file('docbook/fop/fop-osgi.xconf')
      def log4jprops = file('docbook/fop/log4j.properties')
      inputs.files fotask, fopxconf, log4jprops, fileTree('dir': 'docbook', 'include': '**/*.svg')
      inputs.dir fontbasedir
      outputs.file bookpdf
      doFirst {
        project.mkdir(destinationDir)
      }
      doLast {
        def basebook = book - '-diff'
        copy {
          from fopxconf
          into temporaryDir
          filter(ReplaceTokens,
            'tokens': ['book.base.url': file("docbook/${basebook}").toURI().toString(),
              'font.base.url': fontbasedir.toURI().toString(),
              'font.base.dir': fontbasedir.absolutePath])
        }
        javaexec {
          systemProperty('java.awt.headless', 'true')
          systemProperty('JAVA_FONTS', fontbasedir)
          systemProperty('log4j.configuration', log4jprops.toURI())
          classpath compileJava.destinationDir
          classpath bnd.log4j
          classpath fop_classpath
          main = 'org.osgi.tools.fop.FOPMain'
          minHeapSize = '1024m'
          maxHeapSize = '2048m'
          args '-c', new File(temporaryDir, fopxconf.name)
          args '-fo', fotask.bookfo
          args '-pdf', bookpdf
        }.assertNormalExitValue()
      }
    }
  }
}

tasks.addRule('Pattern: <name>.rasterize: Rasterize SVGs for the book <name>.') { taskName ->
  if (taskName.endsWith('.rasterize')) {
    task(taskName) {
      ext.book = taskName - '.rasterize'
      description "Rasterize SVGs for the book ${book}."
      group 'documentation'

      def booktask = tasks.getByName("${book}.xml")
      def imagesXSL = file('docbook/xsl/custom-html-images.xsl')
      ext.destinationDir = new File(buildDir, "images/${book}")

      inputs.files booktask, imagesXSL, fileTree('dir': 'docbook', 'include': '**/*.svg'), sourceSets.main.runtimeClasspath
      outputs.dir destinationDir

      doFirst {
        project.delete(destinationDir)
        project.mkdir(destinationDir)
      }
      doLast {
        def imagesFile = new File(temporaryDir, 'images.txt')
        ant.xslt('style': imagesXSL,
          'in': booktask.bookxml,
          'out': imagesFile) {
          classpath('location': bnd.saxon)
          factory('name': 'com.icl.saxon.TransformerFactoryImpl')
        }

        def svgFiles = fileTree('dir': 'docbook/graphics', 'include': '*draft.svg').files
        imagesFile.eachLine { line ->
          svgFiles << file("docbook/${book}/${line}")
        }

        javaexec {
          systemProperty('scale', '2')
          systemProperty('dpi', '300')
          systemProperty('JAVA_FONTS', file('fonts'))
          classpath sourceSets.main.runtimeClasspath
          main = 'org.osgi.tools.rasterizer.RasterizerMain'
          minHeapSize = '1024m'
          maxHeapSize = '2048m'
          args destinationDir
          args svgFiles
        }.assertNormalExitValue()
      }
    }
  }
}

tasks.addRule('Pattern: <name>.html: Generate the docbook html files for the book <name>.') { taskName ->
  if (taskName.endsWith('.html')) {
    task(taskName) {
      ext.book = taskName - '.html'
      description "Generate the docbook html files for the book ${book}."
      group 'documentation'

      def booktask = tasks.getByName("${book}.xml")
      def rasterizetask = tasks.getByName("${book}.rasterize")
      def stylesheet = file('docbook/xsl/custom-html.xsl')
      def commonXSL = file('docbook/xsl/custom-html-common.xsl')
      def profileXSL = file('docbook/xsl/custom-html-profile.xsl')
      def htmlResources = file('docbook/xsl/html-resources')
      def licensedResources = file("${bnd.licensed}/webresources")
      ext.destinationDir = new File(buildDir, "html/${book}/")

      inputs.files booktask, rasterizetask, stylesheet, commonXSL, profileXSL, htmlResources, licensedResources
      outputs.dir destinationDir

      doFirst {
        project.delete(destinationDir)
        project.mkdir(destinationDir)
      }
      doLast {
        def tmpxml = new File(temporaryDir, 'book.xml')
        ant.xslt('style': profileXSL,
          'in': booktask.bookxml,
          'out': tmpxml,
          'force': true) {
          classpath('location': bnd.saxon)
          factory('name': 'com.icl.saxon.TransformerFactoryImpl')
        }
        ant.xslt('style': stylesheet,
          'in': tmpxml,
          'out': new File(temporaryDir, 'book.txt')) {
          classpath('location': bnd.saxon)
          factory('name': 'com.icl.saxon.TransformerFactoryImpl')
          param('name': 'copyright.year', 'expression': "2000, ${bnd('copyright.year')}")
          param('name': 'draft.watermark.image', 'expression': 'proposed-final-draft.png')
          param('name': 'webhelp.base.dir', 'expression': destinationDir)
          param('name': 'webhelp.default.topic', 'expression': "index.html")
          param('name': 'release.version', 'expression': bnd('osgi.release'))
        }

        copy {
          into destinationDir
          from htmlResources
          from (licensedResources) {
            include '**/*.js'
            into 'js'
            eachFile { fcd ->
              fcd.path = "js/${fcd.name}"
            }
          }
          from (licensedResources) {
            include '**/*.css'
            into 'css'
            eachFile { fcd ->
              fcd.path = "css/${fcd.name}"
            }
          }
          from (rasterizetask) {
            into 'images'
          }
          includeEmptyDirs = false
        }
      }
    }
  }
}

tasks.addRule('Pattern: <name>.zip: Generate a ZIP archive of the docbook html for the book <name>.') { taskName ->
  if (taskName.endsWith('.zip')) {
    task(taskName, type: Zip) {
      ext.book = taskName - '.zip'
      description "Generate a ZIP archive of the docbook html for the book ${book}."
      group 'documentation'
      destinationDir = buildDir
      baseName = "osgi.${book}"
      classifier = 'html'

      from tasks.getByName("${book}.html")
    }
  }
}

task('fontmetrics') {
  description 'Build the font metrics.'
  group 'documentation'
  inputs.files fileTree('dir': file('fonts'), 'include': '*.ttf')
  outputs.files inputs.files.collect {
    it.absolutePath.replaceAll(/\.ttf$/, /.xml/)
  }
  doLast {
    inputs.files.each { ttf ->
      javaexec {
        classpath fop_classpath
        main = 'org.apache.fop.fonts.apps.TTFReader'
        args ttf.absolutePath
        args ttf.absolutePath.replaceAll(/\.ttf$/, /.xml/)
      }.assertNormalExitValue()
    }
  }
}

defaultTasks += 'specifications'

task('specifications') {
  description 'Generates all the specifications.'
  group 'documentation'
  dependsOn 'core.pdf', 'cmpn.pdf', 'enterprise.pdf', 'residential.pdf'
  dependsOn 'core.zip', 'cmpn.zip', 'enterprise.zip', 'residential.zip'
}
